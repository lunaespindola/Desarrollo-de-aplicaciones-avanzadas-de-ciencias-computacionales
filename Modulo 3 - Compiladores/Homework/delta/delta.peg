comments = r'[/][/].*|[/][*](.|\n)*?[*][/]'
identifier = r'[a-zA-Z]\w*'
program = statement* expression EOF
statement = declaration / assignment / if / while / do_while
declaration = 'var' decl_variable (',' decl_variable)* ';'
decl_variable = identifier
assignment = lhs_variable '=' expression ';'
if = 'if' expression block ('else' 'if' expression block)* ('else' block )?
while = 'while' expression block
do_while = 'do' block 'while' expression ';'
block = '{' statement* '}'
lhs_variable = identifier
expression = logical_or ('&&' logical_or)*
logical_or  = comparison ('||' comparison)*
comparison = additive (comparison_operator additive)*
comparison_operator = '==' / '!=' / '>=' / '>' / '<=' / '<'
additive = multiplicative (('+' / '-') multiplicative)*
multiplicative = unary (('*' / '/' / '%') unary)*
unary = ('+' / '-' / '!')* primary
primary = decimal / boolean / binary / hexadecimal / octal / parenthesis / rhs_variable
decimal = r'\d+'
boolean = 'true' / 'false'
binary = r'#b[01]+'
hexadecimal = r'#x[0-9a-fA-F]+'
octal = r'#o[0-7]+'
parenthesis = '(' expression ')'
rhs_variable = identifier
